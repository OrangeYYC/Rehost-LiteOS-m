# Copyright (C) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/communication/netmanager_ext/netmanager_ext_config.gni")

config("net_mdns_manager_if_config") {
  # header file path
  include_dirs = [
    "$BASE_INNERKITS_ROOT/include",
    "$EXT_INNERKITS_ROOT/mdnsclient/include",
    "$EXT_INNERKITS_ROOT/mdnsclient/include/proxy",
    "$NETMANAGER_BASE_ROOT/utils/errorcode_utils/include",
  ]

  cflags = []
  if (is_double_framework) {
    cflags = [ "-DCONFIG_DUAL_FRAMEWORK" ]
  }
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  if (is_standard_system) {
    cflags += [ "-DCONFIG_STANDARD_SYSTEM" ]
  }
  if (defined(build_public_version) && build_public_version) {
    cflags += [ "-DBUILD_PUBLIC_VERSION" ]
  }
}

ohos_source_set("mdns_parcel") {
  sources = [
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/mdns_common.cpp",
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/mdns_service_info.cpp",
  ]

  include_dirs = [ "$EXT_INNERKITS_ROOT/mdnsclient/include" ]

  deps = [
    "$NETMANAGER_BASE_ROOT/utils:net_manager_common",
    "$NETMANAGER_EXT_ROOT/utils:net_manager_ext_common",
  ]

  external_deps = [ "c_utils:utils" ]
  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }

  part_name = "netmanager_ext"
  subsystem_name = "communication"
}

ohos_shared_library("mdns_manager_if") {
  sources = [
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/mdns_client.cpp",
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/proxy/mdns_event_stub.cpp",
    "$NETMANAGER_EXT_ROOT/frameworks/native/mdnsclient/src/proxy/mdns_service_proxy.cpp",
  ]

  public_configs = [ ":net_mdns_manager_if_config" ]

  deps = [
    ":mdns_parcel",
    "$NETMANAGER_BASE_ROOT/utils:net_manager_common",
    "$NETMANAGER_EXT_ROOT/utils:net_manager_ext_common",
  ]

  external_deps = [
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  defines = [
    "NETMGR_LOG_TAG = \"MDnsManager\"",
    "LOG_DOMAIN = 0xD0015B0",
  ]

  if (enable_netmgr_ext_debug) {
    defines += [ "NETMGR_DEBUG" ]
  }

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }

  part_name = "netmanager_ext"
  subsystem_name = "communication"
}
