# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//foundation/communication/dhcp/dhcp_lite.gni")
} else {
  import("//build/ohos.gni")
  import("//foundation/communication/dhcp/dhcp.gni")
}

################################################################################

local_base_sources = [
  "$DHCP_ROOT_DIR/services/mgr_service/src/dhcp_client_service_impl.cpp",
  "$DHCP_ROOT_DIR/services/mgr_service/src/dhcp_event_subscriber.cpp",
  "$DHCP_ROOT_DIR/services/mgr_service/src/dhcp_func.cpp",
  "$DHCP_ROOT_DIR/services/mgr_service/src/dhcp_server_service.cpp",
  "$DHCP_ROOT_DIR/services/mgr_service/src/dhcp_service.cpp",
  "$DHCP_ROOT_DIR/services/mgr_service/src/dhcp_service_api.cpp",
]

local_base_include_dirs = [
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/log",
  "$DHCP_ROOT_DIR/services/mgr_service/include",
  "$DHCP_ROOT_DIR/interfaces/inner_api/interfaces",
  "$DHCP_ROOT_DIR/interfaces/inner_api/include",
]

if (defined(ohos_lite)) {
  shared_library("dhcp_manager_service") {
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
      "//third_party/bounds_checking_function/include",
    ]

    deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
    ]

    defines = [
      "_GNU_SOURCE",
      "OHOS_ARCH_LITE",
    ]
    configs -= [ "//build/lite/config:language_cpp" ]
    cflags_cc = [
      "-fPIC",
      "-std=c++17",
      "-Wall",
    ]
    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]
  }
} else {
  ohos_shared_library("dhcp_manager_service") {
    sanitize = {
      cfi = true
      boundary_sanitize = true
      cfi_cross_dso = true
    }
    install_enable = true
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//commonlibrary/c_utils/base/include",
      "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
    ]

    cflags_cc = [
      "-std=c++17",
      "-Wall",
    ]

    external_deps = [
      "ability_base:want",
      "bundle_framework:appexecfwk_base",
      "c_utils:utils",
      "common_event_service:cesfwk_innerkits",
      "eventhandler:libeventhandler",
      "hiviewdfx_hilog_native:libhilog",
    ]
    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]

    part_name = "dhcp"
    subsystem_name = "communication"
  }

  ohos_shared_library("dhcp_updater_manager") {
    sanitize = {
      cfi = true
      boundary_sanitize = true
      cfi_cross_dso = true
    }
    install_enable = true
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//commonlibrary/c_utils/base/include",
      "//third_party/bounds_checking_function/include",
    ]

    cflags_cc = [
      "-std=c++17",
      "-Wall",
    ]

    defines = [ "OHOS_ARCH_LITE" ]

    deps = [ "//third_party/bounds_checking_function:libsec_shared" ]

    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]
    install_images = [ "updater" ]
    part_name = "dhcp"
    subsystem_name = "communication"
  }
}
