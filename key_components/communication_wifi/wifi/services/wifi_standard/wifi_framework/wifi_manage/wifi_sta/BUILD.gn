# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//foundation/communication/wifi/wifi/wifi_lite.gni")
} else {
  import("//build/ohos.gni")
  import("//foundation/communication/wifi/wifi/wifi.gni")
}

local_base_sources = [
  "../common/handler.cpp",
  "../common/internal_message.cpp",
  "../common/message_queue.cpp",
  "../common/state.cpp",
  "../common/state_machine.cpp",
  "sta_auto_connect_service.cpp",
  "sta_interface.cpp",
  "sta_monitor.cpp",
  "sta_network_check.cpp",
  "sta_saved_device_appraisal.cpp",
  "sta_service.cpp",
  "sta_state_machine.cpp",
]

local_base_include_dirs = [
  "$WIFI_ROOT_DIR/services/wifi_standard/sdk/include",
  "$WIFI_ROOT_DIR/frameworks/native/interfaces",
  "$WIFI_ROOT_DIR/services/wifi_standard/ipc_framework/interface",
  "$WIFI_ROOT_DIR/services/wifi_standard/ipc_framework/cRPC/include",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/include",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/net_helper",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/log",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/utils",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit/config",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/common",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_scan",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client/idl_interface",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_sta/net_conf",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_sta",
  "$DHCP_ROOT_DIR/services/mgr_service/include",
  "$DHCP_ROOT_DIR/interfaces/inner_api/include",
  "$DHCP_ROOT_DIR/interfaces/inner_api/interfaces",
  "$WIFI_ROOT_DIR/utils/inc",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage",
  "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_common",
  "$WIFI_ROOT_DIR/services/wifi_standard/include",
]

if (defined(ohos_lite)) {
  shared_library("wifi_sta_service") {
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
      "//third_party/bounds_checking_function/include",
    ]

    deps = [
      "$DHCP_ROOT_DIR/services/mgr_service:dhcp_manager_service",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage:wifi_service_base",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client:wifi_idl_client",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit:wifi_toolkit",
      "$WIFI_ROOT_DIR/utils:wifi_utils",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
    ]

    configs -= [ "//build/lite/config:language_cpp" ]
    cflags_cc = [
      "-std=c++17",
      "-fno-rtti",
    ]

    defines = [ "OHOS_ARCH_LITE" ]
    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]
  }
} else {
  ohos_shared_library("wifi_sta_service") {
    sanitize = {
      cfi = true
      boundary_sanitize = true
      cfi_cross_dso = true
    }
    install_enable = true
    sources = local_base_sources

    include_dirs = local_base_include_dirs
    include_dirs += [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
      "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
      "//foundation/systemability/safwk/interfaces/innerkits/safwk",
      "//commonlibrary/c_utils/base/include",
    ]

    cflags_cc = [
      "-std=c++17",
      "-fno-rtti",
    ]

    ldflags = [
      "-fPIC",
      "-Wl,-E",
    ]

    deps = [
      "$DHCP_ROOT_DIR/services/mgr_service:dhcp_manager_service",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage:wifi_manager_service",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/idl_client:wifi_idl_client",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_manage/wifi_common:wifi_common_service",
      "$WIFI_ROOT_DIR/services/wifi_standard/wifi_framework/wifi_toolkit:wifi_toolkit",
      "$WIFI_ROOT_DIR/utils:wifi_utils",
    ]

    external_deps = [
      "c_utils:utils",
      "hiviewdfx_hilog_native:libhilog",
      "ipc:ipc_core",
      "netmanager_base:net_conn_manager_if",
      "samgr:samgr_proxy",
    ]

    part_name = "wifi"
    subsystem_name = "communication"
  }
}
